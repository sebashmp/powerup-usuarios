plugins {
	id 'org.springframework.boot' version '2.7.3'
	id 'io.spring.dependency-management' version '1.0.13.RELEASE'
	id 'java'
	id 'jacoco'
}

group = 'com.pragma.powerup'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	springBootVersion = "2.7.3"
	mapstructVersion = "1.5.2.Final"
	lombokVersion = "1.18.24"
	lombokMapstructBindingVersion = "0.2.0"
	mySqlConnectorVersion = "8.0.32"
	openApiVersion = "1.6.11"
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
	developmentOnly "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"

	runtimeOnly "mysql:mysql-connector-java:${mySqlConnectorVersion}"

	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

	implementation "org.springdoc:springdoc-openapi-ui:${openApiVersion}"
	implementation 'io.github.cdimascio:dotenv-java:2.2.4'

	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
}

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test

	reports {
		xml.required = true
		csv.required = false
		html.required = true
	}

	classDirectories.setFrom(
			files(classDirectories.files.collect { dir ->
				fileTree(dir: dir, exclude: [
						'**/infrastructure/**'
				])
			})
	)
}